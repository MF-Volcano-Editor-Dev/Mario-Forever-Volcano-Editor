class_name AreaFluid extends Area2D

## [AreaFluid] is an field in which characters is able to swim.
##
## [b]Note:[/b] This node is only avaiable for [Character].
## For modification on general [EntityBody2D]s, see [member Area2D.gravity_space_override]

@export_group("For Character", "character_")
## Scales for properties, going to be modified, of a characters that.[br]
## [br]
## [b]Note:[/b] The keys are of [NodePath] or [String] type while the values belong to one of the numeric types.[br]
## [br]
## [b]Warning:[/b] Only numberic types are supported; otherwise, an error would be thrown!
@export_range(0, 1, 0.001, "or_greater", "hide_slider", "suffix:x") var character_max_falling_speed_scale: float = 1.0
@export_group("Spray")
## Spray generated by the fluid
@export var spray: PackedScene
## Determines that only those bodies in node group [member spray_effect_group] triggers spray generation.
## If this is empty, then each body is allowed to trigger the creation of spray.
@export var spray_effect_group: StringName

var _characters: Array[Character]


func _init() -> void:
	process_physics_priority = -128 # Needs this line to make sure the _property_update() will be called before all nodes in the current scene

func _ready() -> void:
	body_entered.connect(_on_body_entered)
	body_exited.connect(_on_body_exited)

func _physics_process(_delta: float) -> void:
	_property_update() # This will be called BEFORE all nodes get the virtual method called
	_property_revert.call_deferred() # This will be called AFTER all nodes get the virtual method called


#region == Updating and Reverting Data ==
func _property_update() -> void:
	for i in _characters:
		i.max_falling_speed *= character_max_falling_speed_scale

func _property_revert() -> void:
	for i in _characters:
		i.max_falling_speed /= character_max_falling_speed_scale
#endregion

func _spray(body: Node2D) -> void:
	if !spray:
		return
	if !is_instance_valid(body):
		return
	if !body is PhysicsBody2D:
		return
	if !spray_effect_group.is_empty() && !body.is_in_group(spray_effect_group):
		return
	
	var e: Node2D = spray.instantiate()
	e.global_transform = body.global_transform
	
	add_sibling.call_deferred(e)


#region == Area Detections ==
func _on_body_entered(body: Node2D) -> void:
	# Ready requires 5 frames
	# To prevent from creating a large amount of sprays
	if get_tree().get_frame() > 5:
		_spray.call_deferred(body)
	if body is Character && !body in _characters:
		_characters.append(body)

func _on_body_exited(body: Node2D) -> void:
	if get_tree().get_frame() > 5 && body.is_inside_tree():
		_spray.call_deferred(body)
	if body is Mario && body in _characters:
		_characters.erase(body)
#endregion
